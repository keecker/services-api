ext {
  ARTIFACT_GROUP_ID = "com.keecker.services"
  ARTIFACT_ID = "interfaces"
}

buildscript {
  repositories {
    mavenLocal()
    google()
    maven {
      url 'http://maven.keecker.com/artifactory/internal-snapshot'
      name 'keecker'
      credentials {
        username = "${artifactory_user}"
        password = "${artifactory_password}"
      }
    }
  }

  ext.kotlin_version = '1.2.71'

  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.4'
    classpath 'digital.wup:android-maven-publish:3.6.2'
    classpath 'com.squareup.wire:wire-compiler:2.0.4-SNAPSHOT-jar-with-dependencies'
    classpath 'com.squareup.wire:wire-gradle-plugin:1.0.0-SNAPSHOT'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

allprojects {
  repositories {
    mavenLocal()
    google()
    jcenter()
    maven {
      url 'http://maven.keecker.com/artifactory/internal-snapshot'
      name 'keecker'
      credentials {
        username = "${artifactory_user}"
        password = "${artifactory_password}"
      }
    }
  }

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << '-Xlint:deprecation'
      options.compilerArgs << '-Xlint:unchecked'
    }
  }
}

// Help functions

def gitTag() {
  def tag = ''
  def proc = 'git describe --abbrev=0 --tags'.execute()
  proc.in.eachLine { line -> tag = line }
  proc.err.eachLine { line -> println line }
  proc.waitFor()
  tag
}

def verNum  = gitTag()
println "verNum: $verNum"
def verVals = verNum.split('\\.')
println "verVals: $verVals"
int verCode = (verVals[0].toInteger()*10000 + verVals[1].toInteger()*100 + verVals[2]) as Integer
println "verCode: $verCode"

// Library definition

apply plugin: 'com.android.library'
apply plugin: 'com.squareup.wire'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 27

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 27
    versionCode 1
    versionName '1.0'
    multiDexEnabled true
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    ndk { abiFilters 'arm64-v8a' }
  }

  externalNativeBuild { cmake { path "CMakeLists.txt" } }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  aidlPackageWhiteList "com/keecker/services/utils/IIpcSubscriber.aidl"
  aidlPackageWhiteList "com/keecker/services/utils/sharedmemory/ISharedMemorySubscriber.aidl"
  aidlPackageWhiteList "com/keecker/hardware/robot/interfaces/MovementSafetiesStatus.aidl"
  aidlPackageWhiteList "com/keecker/services/utils/ILowBatteryNotificationListener.aidl"
  aidlPackageWhiteList "com/keecker/services/navigation/interfaces/ITrackedPoseListener.aidl"
}

dependencies {
  implementation 'com.squareup.wire:wire-runtime:2.0.3'

  compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.8.9'
  androidTestImplementation 'com.android.support.test:rules:1.0.1'
}

archivesBaseName = "$ARTIFACT_GROUP_ID.$ARTIFACT_ID"

apply plugin: 'digital.wup.android-maven-publish'
publishing {
  publications {
    android.libraryVariants.all { variant ->
      "maven${variant.name.capitalize()}Aar"(MavenPublication) {
        from components.findByName("android${variant.name.capitalize()}")
        groupId "${ARTIFACT_GROUP_ID}"
        artifactId "${ARTIFACT_ID}"
        version "${verNum}-${variant.name}"
      }
    }
  }

  repositories {
    maven {
      url 'http://maven.keecker.com/artifactory/internal-snapshot-local'
      name 'keecker'
      credentials {
        username = "${artifactory_user}"
        password = "${artifactory_password}"
      }
    }
  }
}

